[case testIsNone]
from typing import Optional

class A: pass

def f(x: Optional[A]) -> int:
    if x is None:
        return 1
    return 2
[out]
def f(x):
    x :: optional[A]
    r0 :: bool
    r1, r2 :: int
L0:
    r0 = x is None
    if r0 goto L1 else goto L2 :: bool
L1:
    r1 = 1
    return r1
L2:
    r2 = 2
    return r2

[case testIsNotNone]
from typing import Optional

class A: pass

def f(x: Optional[A]) -> int:
    if x is not None:
        return 1
    return 2
[out]
def f(x):
    x :: optional[A]
    r0, r1 :: bool
    r2, r3 :: int
L0:
    r0 = x is None
    r1 = !r0
    if r1 goto L1 else goto L2 :: bool
L1:
    r2 = 1
    return r2
L2:
    r3 = 2
    return r3

[case testAssignToOptional]
from typing import Optional

class A:
    a: Optional[int]

def f(x: Optional[A], y: Optional[A], z: Optional[int]) -> None:
    x = None
    x = A()
    x = y
    z = 1
    a = A()
    a.a = 1
    a.a = None
[out]
def f(x, y, z):
    x, y :: optional[A]
    z :: optional[int]
    r0 :: None
    r1 :: A
    r2 :: int
    r3 :: object
    r4, a :: A
    r5 :: int
    r6 :: object
    r7 :: bool
    r8 :: None
    r9 :: bool
    r10 :: None
L0:
    r0 = None
    x = r0
    r1 = A()
    x = r1
    x = y
    r2 = 1
    r3 = box(int, r2)
    z = r3
    r4 = A()
    a = r4
    r5 = 1
    r6 = box(int, r5)
    a.a = r6; r7 = is_error
    r8 = None
    a.a = r8; r9 = is_error
    r10 = None
    return r10

[case testBoxOptionalListItem]
from typing import List, Optional

def f(x: List[Optional[int]]) -> None:
    x[0] = 0
    x[1] = None
[out]
def f(x):
    x :: list
    r0, r1 :: int
    r2 :: object
    r3 :: bool
    r4 :: None
    r5 :: int
    r6 :: bool
    r7 :: None
L0:
    r0 = 0
    r1 = 0
    r2 = box(int, r0)
    r3 = x.__setitem__(r1, r2) :: list
    r4 = None
    r5 = 1
    r6 = x.__setitem__(r5, r4) :: list
    r7 = None
    return r7

[case testNarrowDownFromOptional]
from typing import Optional

class A: pass

def f(x: Optional[A]) -> A:
    y = A()
    if x is not None:
        y = x
        return x
    return y
[out]
def f(x):
    x :: optional[A]
    r0, y :: A
    r1, r2 :: bool
    r3, r4 :: A
L0:
    r0 = A()
    y = r0
    r1 = x is None
    r2 = !r1
    if r2 goto L1 else goto L2 :: bool
L1:
    r3 = cast(A, x)
    y = r3
    r4 = cast(A, x)
    return r4
L2:
    return y

[case testPartialOptionalType]
def f(y: int) -> None:
    x = None
    if y == 1:
        x = y
    if x is not None:
        y = x
[out]
def f(y):
    y :: int
    r0 :: None
    x :: optional[int]
    r1 :: int
    r2 :: bool
    r3 :: object
    r4, r5 :: bool
    r6 :: int
    r7 :: None
L0:
    r0 = None
    x = r0
    r1 = 1
    r2 = y == r1 :: int
    if r2 goto L1 else goto L2 :: bool
L1:
    r3 = box(int, y)
    x = r3
L2:
    r4 = x is None
    r5 = !r4
    if r5 goto L3 else goto L4 :: bool
L3:
    r6 = unbox(int, x)
    y = r6
L4:
    r7 = None
    return r7

[case testUnionType]
from typing import Union

class A:
    a: int

def f(x: Union[int, A]) -> int:
    if isinstance(x, int):
        return x + 1
    else:
        return x.a
[out]
def f(x):
    x :: union[int, A]
    r0 :: object
    r1 :: str
    r2 :: object
    r3 :: bool
    r4, r5, r6 :: int
    r7 :: A
    r8 :: int
L0:
    r0 = builtins.module :: static
    r1 = unicode_0 :: static  ('int')
    r2 = getattr r0, r1
    r3 = isinstance x, r2
    if r3 goto L1 else goto L2 :: bool
L1:
    r4 = unbox(int, x)
    r5 = 1
    r6 = r4 + r5 :: int
    return r6
L2:
    r7 = cast(A, x)
    r8 = r7.a
    return r8
L3:
    unreachable

[case testUnionTypeInList]
from typing import List, Union

def f(x: List[Union[int, str]]) -> object:
    return x[0]
[out]
def f(x):
    x :: list
    r0 :: int
    r1 :: object
    r2 :: union[int, str]
L0:
    r0 = 0
    r1 = x[r0] :: list
    r2 = cast(union[int, str], r1)
    return r2

[case testUnionTypeAttributeAccess]
from typing import Union
class A:
    a: str
    def f(self, x: int) -> None: pass
class B:
    a: object
    def f(self, x: int) -> None: pass
def g1(o: Union[A, B]) -> None:
    o.f(1)
def g2(o: Union[A, B]) -> None:
    o.a
def g3(o: Union[A, B], s: str) -> None:
    o.a = s

[out]
def A.f(self, x):
    self :: A
    x :: int
    r0 :: None
L0:
    r0 = None
    return r0
def B.f(self, x):
    self :: B
    x :: int
    r0 :: None
L0:
    r0 = None
    return r0
def g1(o):
    o :: union[A, B]
    r0 :: int
    r1 :: str
    r2, r3 :: object
    r4, r5 :: None
L0:
    r0 = 1
    r1 = unicode_0 :: static  ('f')
    r2 = box(int, r0)
    r3 = py_method_call(o, r1, r2)
    r4 = cast(None, r3)
    r5 = None
    return r5
def g2(o):
    o :: union[A, B]
    r0, r1 :: object
    r2 :: bool
    r3 :: A
    r4 :: str
    r5 :: B
    r6 :: object
    r7 :: None
L0:
    r1 = __main__.A :: type
    r2 = isinstance o, r1
    if r2 goto L1 else goto L2 :: bool
L1:
    r3 = cast(A, o)
    r4 = r3.a
    r0 = r4
    goto L3
L2:
    r5 = cast(B, o)
    r6 = r5.a
    r0 = r6
L3:
    r7 = None
    return r7
def g3(o, s):
    o :: union[A, B]
    s, r0 :: str
    r1 :: bool
    r2 :: None
L0:
    r0 = unicode_1 :: static  ('a')
    r1 = setattr o, r0, s
    r2 = None
    return r2
